#!/bin/csh
setenv LMOD_CMD    "@PKGV@/libexec/lmod"
setenv LMOD_DIR    "@PKGV@/libexec"
setenv LMOD_PKG    "@PKGV@"
setenv MODULESHOME "@PKGV@"

set prefix  = ""
set midfix  = ""
set postfix = ";"

set histchar = \!
if ( $?histchars) then
  set histchar   = `echo $histchars | cut -c1`
  if ( "$histchar" != '\!' ) then
    set _histchars = $histchars
    set prefix  = 'set _histchars = $histchars; unset histchars;'
    set midfix  = '--localvar histchars=$_histchars'
    set postfix = ';set histchars = $_histchars'
  endif
endif

if ($?tcsh) then
  alias module  $prefix'eval `$LMOD_CMD csh '$midfix $histchar'*` '$postfix' eval `$LMOD_SETTARG_CMD -s csh`'
else
  alias module  $prefix'eval `$LMOD_CMD csh '$midfix $histchar'*` '$postfix
endif
alias clearMT 'eval `'$LMOD_DIR/clearMT_cmd csh'`'

alias ml      'eval `'$LMOD_DIR/ml_cmd' '$histchar'*`'

setenv LMOD_VERSION '@lmod_version@'

unset prefix
unset midfix
unset postfix
unset histchar


if ( ! $?TARG_TITLE_BAR_PAREN ) then
  setenv TARG_TITLE_BAR_PAREN ""
endif

if ( $?TERM && $?prompt && $?tcsh && "$LMOD_FULL_SETTARG_SUPPORT" != no ) then
   alias cwdcmd 'eval `$LMOD_SETTARG_CMD -s csh`'

   switch ($TERM)
      case "xterm*":
         set echo_style=both
         alias precmd 'echo -n "\033]2;${TARG_TITLE_BAR_PAREN}${HOST}:$cwd\007"'
         breaksw
      endsw
   endif
endif
# Local Variables:
# mode: shell-script
# indent-tabs-mode: nil
# end:

